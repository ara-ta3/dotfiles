function peco-select-history() {
  local tac
  if which tac > /dev/null; then
    tac="tac"
  else
    tac="tail -r"
  fi
  BUFFER=$(\history -n 1| eval $tac | peco --query "$LBUFFER")
  CURSOR=$#BUFFER
  zle clear-screen
}

function peco-path() {
  local filepath="$(find . | grep -v '/\.' | peco --prompt 'PATH>')"
  [ -z "$filepath" ] && return
  if [ -n "$LBUFFER" ]; then
      BUFFER="$LBUFFER$filepath"
  else
      if [ -d "$filepath" ]; then
          BUFFER="cd $filepath"
      elif [ -f "$filepath" ]; then
          BUFFER="$EDITOR $filepath"
      fi
  fi
  CURSOR=$#BUFFER
}

function ghq-peco () {
    local selected_dir=$(ghq list -p | peco --query "$LBUFFER")
    if [ -n "$selected_dir" ]; then
        BUFFER="cd ${selected_dir}"
        zle accept-line
    fi
}

function ghqdir () {
    cd $(ghq root)/github.com/ara-ta3
}

function godir () {
    cd $GOPATH/src/github.com/ara-ta3
}

function hub-browse () {
    hub browse $(ghq list|peco|cut -d "/" -f 2,3)
}

function git-get () {
    ghq get $1
}

function do_enter() {
    if [ -n "$BUFFER" ]; then
        zle accept-line
        return 0
    fi
    echo
    echo -e "\e[0;33m--- ls ---\e[0m"
    which lsd > /dev/null && lsd -Fa --blocks date --blocks size --blocks name --header|| ls -l
    if [ "$(git rev-parse --is-inside-work-tree 2> /dev/null)" = 'true' ]; then
        echo 
        echo -e "\e[0;33m--- git status ---\e[0m"
        git status -sb
    fi
    echo "

    "
    zle reset-prompt
    return 0
}

function documents-peco () {
    local ds="$(echo $HOME/Documents/Workspace)\n$(find $HOME/Documents/Workspace -type d -depth 2)"
    local selected_dir=$(echo $ds|sort|peco --query "$LBUFFER")
    if [ -n "$selected_dir" ]; then
        BUFFER="cd ${selected_dir}"
        zle accept-line
    fi
}

zle -N documents-peco
bindkey '^b' documents-peco

zle -N do_enter
zle -N peco-path
zle -N peco-select-history
zle -N ghq-peco
zle -N ghqdir
zle -N hub-browse
bindkey '^r' peco-select-history
bindkey '^f' peco-path 
bindkey '^G' ghq-peco
bindkey '^H^H' hub-browse
bindkey "\e[Z" reverse-menu-complete
bindkey '^m' do_enter
